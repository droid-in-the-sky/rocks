#include "sdl.ceu"
#include "sdl-colors.ceu"

#define WIN_W 1280
#define WIN_H  800

#include "snds.ceu"

watching SDL_go("Rocks!", WIN_W,WIN_H, _) => (ren) do
    #include "texs.ceu"
    watching Texs() => (tex_bg, tex_tap, tex_nums)
           , Snds() => (snd_start)
    do
        par do
            every SDL_REDRAW do
                _SDL_RenderCopy(ren, tex_bg.ptr, null, null);
            end
        with
            #include "points.ceu"
            var int x = WIN_W / 15;
            var int y = WIN_H - WIN_H/10;
            watching Points(SDL_Point(x,      y)) => (points1)
                   , Points(SDL_Point(WIN_W-x,y)) => (points2)
            do
                par do
                    every 1s do
                        emit points1.inc;
                    end
                with
                    every 2s do
                        emit points2.inc;
                    end
                with
                    loop do
                        var SDL_Rect r = val SDL_Rect(WIN_W/2 - tex_tap.width/2,
                                                      WIN_H/2 - tex_tap.height/2,
                                                      tex_tap.width, tex_tap.height);

                        par/or do
                            await 1s;
                            await SDL_KEYDOWN;
                        with
                            loop do
                                await 500ms;
                                watching 500ms do
                                    every SDL_REDRAW do
                                        _SDL_RenderCopy(ren, tex_tap.ptr, null,
                                                        (&&r as _SDL_Rect&&));
                                    end
                                end
                            end
                        end
                        _Mix_PlayChannel(-1, snd_start, 0);
await 2s;
                    end
                end
            end
        end
    end
end

escape 0;
