/*
 * "TextPoints" implements the "Points" interface.
 * Starting from "0", it shows the total of points in the requested position 
 * "pos".
 * It also continuously awaits "go_inc" to increment the points and recreate 
 * the text to show on screen.
 */
class TextPoints with
    interface Points;   // implements "Points" (pos, go_inc)
    var int v = 0;
do
    /*
     * Initial value, on requested position.
     */
    var int v0=0, v1=0, v2=0;

    var SDL_Rect rect = SDL_Rect(this.pos.x,this.pos.y, 0,0);
                                    // centered position
    _SDL_QueryTexture(_TEX_NUM[0], null, null, &&rect.w, &&rect.h);

    par do
        every this.go_inc do
            v = v + 1;
            v = v % 1000;
            v0 =     v % 10;
            v1 =  v/10 % 10;
            v2 = v/100 % 10;
        end
    with
        every SDL_REDRAW do
            var SDL_Rect r = rect;
            _SDL_RenderCopy(_REN, _TEX_NUM[v0], null, (_SDL_Rect&&)&&r);
            if v >= 10 then
                r.x = r.x - r.w;
                _SDL_RenderCopy(_REN, _TEX_NUM[v1], null, (_SDL_Rect&&)&&r);
                if v >= 100 then
                    r.x = r.x - r.w;
                    _SDL_RenderCopy(_REN, _TEX_NUM[v2], null, (_SDL_Rect&&)&&r);
                end
            end
        end
    end
end
