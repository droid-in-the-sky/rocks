data Orientation;
data Orientation.Right is  1;
data Orientation.Left  is -1;

data Axis with
    var float s;
    var float v;
    var float a;
end

data Movable with
    var Axis x;
    var Axis y;
end

data Collideable with
    var& SDL_Rect rect;
    event& void go;
end

code/await Move (var& SDL_Rect rect, var& Movable mov) => void
do
    loop do
        var int dt = await SDL_DT;

        mov.x.v = mov.x.v + mov.x.a*dt;
        mov.x.s = mov.x.s + mov.x.v*dt/1000;
        rect.x = mov.x.s as int;

        mov.y.v = mov.y.v + mov.y.a*dt;
        mov.y.s = mov.y.s + mov.y.v*dt/1000;
        rect.y = mov.y.s as int;

        if mov.x.s<0 or mov.x.s>WIN_W then
            break;
        end
        if mov.y.s<0 or mov.y.s>WIN_H then
            break;
        end
    end
end
