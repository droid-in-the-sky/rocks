code/await Rock_Big (
        var& SDL_Texture tex,
    ) => (
        var& Object obj_,
    ) =>void
do
    var Object obj = val Object.Rock(SDL_Rect(_,_, tex.height,tex.height),
                                     Orientation(), false);
    obj_ = &obj;

    var float vx = _rand()%(WIN_W/5) + 1;
    var float vy = _rand()%(WIN_H/5) + 1;
    do
        obj.rect.x = WIN_W/3 + _rand()%WIN_W/3;
        if (_rand()%2) as bool then
            vx = -vx;
        end
        if (_rand()%2) as bool then
            obj.rect.y = 0;
        else
            obj.rect.y = WIN_H;
            vy = -vy;
        end
    end

    var Movable mov = val Movable(Axis(obj.rect.x, vx, 0),
                                  Axis(obj.rect.y, vy, 0));

    watching Move(&obj,&mov) do
        var SDL_Rect clip = val SDL_Rect(0,0, tex.height,tex.height);
        par do
            var float v = _sqrt(mov.x.v*mov.x.v + mov.y.v*mov.y.v);
            var float p = 0;
            var int dt;
            every dt in SDL_DT do
                p = p + v*dt/1000;
                clip.x = ((p/obj.rect.w) as int)*obj.rect.w % (6*obj.rect.w);
            end
        with
            every SDL_REDRAW do
                var SDL_Rect r = val SDL_Rect(obj.rect.x-obj.rect.w/2,
                                              obj.rect.y-obj.rect.h/2,
                                              obj.rect.w, obj.rect.h);
                _SDL_RenderCopy(outer.ren, tex.ptr, (&&clip as _SDL_Rect&&),
                                                    (&&r as _SDL_Rect&&));
            end
        end
    end
end
